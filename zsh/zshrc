PATH="$PATH:$HOME/usr/bin"
LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$HOME/usr/lib"
EDITOR=vim

autoload -U compinit && compinit
autoload -U promptinit && promptinit
autoload -U colors && colors
autoload -U select-quoted
autoload -U select-bracketed

zle -N select-quoted
zle -N select-bracketed
zle -N zle-line-init
zle -N zle-keymap-select

setopt extended_glob
unsetopt correct_all

setopt menu_complete
zmodload zsh/complist

export HISTFILE="$HOME/.zsh_history"
export HISTSIZE=3000
export SAVEHIST=3000
setopt append_history
setopt extended_history
setopt inc_append_history
setopt hist_ignore_all_dups
setopt share_history

for mode in visual viopp; do
  for keymap in {a,i}{\',\",\`}; do
    bindkey -M "$mode" "$keymap" select-quoted
  done
done

for mode in visual viopp; do
  for keymap in {a,i}${(s..)^:-'()[]{}<>bB'}; do
    bindkey -M "$mode" "$keymap" select-bracketed
  done
done

function zle-line-init zle-keymap-select
{
  HIGHLIGHT="%{$bg[white]$fg[black]%}"
  VIM_MODE="${${KEYMAP/vicmd/$HIGHLIGHT>>>}/(main|viins)/>>>}"
  PS1="%{$fg_bold[white]$bg[blue]%}%n@%m%{$reset_color%} %{$fg_bold[yellow]%}%~%{$reset_color%}${prompt_newline}${VIM_MODE}%{$reset_color%} "
  zle reset-prompt
}

zstyle ':completion:*' menu select=1
zstyle ':completion:*' matcher-list '' \
  'm:{a-z\-}={A-Z\_}' \
  'r:[^[:alpha:]]||[[:alpha:]]=** r:|=* m:{a-z\-}={A-Z\_}' \
  'r:[[:ascii:]]||[[:ascii:]]=** r:|=* m:{a-z\-}={A-Z\_}'
zstyle ':completion:*' completer _complete _approximate
zstyle ':completion:*:approximate:::' max-errors 1 numeric
zstyle -e ':completion:*:default' list-colors 'reply=("${PREFIX:+=(#bi)($PREFIX:t)(?)*==02=01}:${(s.:.)LS_COLORS}")'
zstyle ':completion:*:*:*:*:processes' command 'ps -u $USER -o pid,user,command -w -w'
zstyle ':completion:*' file-sort modification
bindkey -M menuselect '^[[Z' reverse-menu-complete

bindkey -v
bindkey -M viins 'jk' vi-cmd-mode
bindkey "^?" backward-delete-char

source "$HOME/.zsh/plugins/zsh-history-substring-search/zsh-history-substring-search.zsh"
bindkey -M vicmd 'j' history-substring-search-down
bindkey -M vicmd 'k' history-substring-search-up

source "$HOME/.zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets pattern cursor)
ZSH_HIGHLIGHT_STYLES[unknown-token]='fg=red,bold'
ZSH_HIGHLIGHT_STYLES[alias]='fg=green,bold'
ZSH_HIGHLIGHT_STYLES[builtin]='fg=green,bold'
ZSH_HIGHLIGHT_STYLES[function]='fg=green,bold'
ZSH_HIGHLIGHT_STYLES[command]='fg=green,bold'
ZSH_HIGHLIGHT_STYLES[path]='fg=white,bold'
ZSH_HIGHLIGHT_STYLES[path_prefix]='fg=white,underline'
ZSH_HIGHLIGHT_STYLES[globbing]='fg=gray'

alias y='vim /tmp/tmux_panel.txt -c "normal! Gkk"'
alias ls='ls -Fa'
alias tmux="TERM=screen-256color-bce tmux"
alias xc='xclip -selection c'
